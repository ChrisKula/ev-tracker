import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

class Version {
    int major, minor, patch

    String versionName() {
        "$major.$minor.$patch"
    }

    int versionCode() {
        major * 1000 + minor * 100 + patch
    }
}

def version = new Version(
        major: 0,
        minor: 1,
        patch: 0)

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new SimpleDateFormat("dd_MM_yyyy-HH:mm:ss", Locale.ENGLISH).format(new Date()).toUpperCase()

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.christiankula.evtracker"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode version.versionCode()
        versionName version.versionName()
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:palette-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    testCompile 'junit:junit:4.12'
}
